plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.jesusbadenas.kotlin_clean_architecture_project"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':data')
    api project(':domain')

    implementation rootProject.ext.androidAppCompat
    implementation rootProject.ext.androidConstraintLayout
    implementation rootProject.ext.androidCoreKtx
    implementation rootProject.ext.androidMaterial
    implementation rootProject.ext.archCoreCommon
    implementation rootProject.ext.coroutinesAndroid
    implementation rootProject.ext.coroutinesCore
    implementation rootProject.ext.fragment
    implementation rootProject.ext.glide
    implementation rootProject.ext.koinAndroid
    implementation rootProject.ext.koinFragment
    implementation rootProject.ext.koinViewModel
    implementation rootProject.ext.kotlin
    implementation rootProject.ext.lifeCycleExtensions
    implementation rootProject.ext.lifeCycleLiveDataKtx
    implementation rootProject.ext.lifeCycleViewModelKtx
    implementation rootProject.ext.loggingInterceptor
    implementation rootProject.ext.multidex
    implementation rootProject.ext.navigationFeature
    implementation rootProject.ext.navigationFragment
    implementation rootProject.ext.navigationUi
    implementation rootProject.ext.recyclerView
    implementation rootProject.ext.retrofit
    implementation rootProject.ext.swipeRefreshLayout
    implementation rootProject.ext.timber

    kapt rootProject.ext.dataBindingCompiler
    kapt rootProject.ext.glideCompiler
    kapt rootProject.ext.lifeCycleCompiler

    testImplementation project(':test')
    testImplementation rootProject.ext.archCoreTesting
    testImplementation rootProject.ext.coroutinesTest
    testImplementation rootProject.ext.jUnit
    testImplementation rootProject.ext.mockk

    androidTestImplementation project(':test')
    androidTestImplementation rootProject.ext.androidXJUnitKtx
    androidTestImplementation rootProject.ext.androidXTestCoreKtx
    androidTestImplementation rootProject.ext.androidXTestRules
    androidTestImplementation rootProject.ext.androidXTestRunner
    androidTestImplementation rootProject.ext.coroutinesTest
    androidTestImplementation rootProject.ext.espresso
    androidTestImplementation rootProject.ext.espressoIntents
    androidTestImplementation rootProject.ext.fragmentTest
    androidTestImplementation rootProject.ext.mockkAndroid
    androidTestImplementation rootProject.ext.navigationTest
}
